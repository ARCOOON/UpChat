static def getVersionCode(versionName) {
    def parts = versionName.split(/[-.]/)
    def major = parts[0] as int
    def minor = parts[1] as int
    def patch = parts[2] as int
    return major * 10000 + minor * 100 + patch
}

plugins {
    id('com.android.application')
    id('org.jetbrains.kotlin.android')
    id('com.google.gms.google-services')
    id('com.google.firebase.crashlytics')
}

android {
    namespace = 'com.devusercode.upchat'
    compileSdkVersion = 36

    defaultConfig {
        applicationId 'com.devusercode.upchat'
        minSdk 24
        targetSdk 34
        versionName '1.3.1-alpha'
        versionCode getVersionCode(versionName)

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        // debug {
        //    storeFile file('C:\\Users\\leon\\Desktop\\UpChat\\debug-keystore.jks')
        //    storePassword 'master'
        //    keyAlias 'DebuggingKey'
        //    keyPassword 'master'
        // }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            // signingConfig signingConfigs.debug
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }

        debug {
            minifyEnabled false
            debuggable true
            // signingConfig signingConfigs.debug
        }

        /*
        applicationVariants.all { variant ->
            variant.outputs.all {
                var buildType = variant.name // release | debug
                var version = variant.versionName
                outputFileName = "upchat-${version}-${buildType}.apk"
            }
        }
         */
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        // coreLibraryDesugaringEnabled true
    }

    kotlin {
        jvmToolchain(17)
    }

    buildToolsVersion '35.0.1'
}

androidComponents {
    onVariants(selector().all()) { variant ->
        def buildType = variant.buildType  // "debug" | "release"

        variant.outputs.forEach { output ->
            // Provider<String>; handle absence safely
            def version = output.versionName.isPresent() ? output.versionName.get() : "dev"
            output.outputFileName.set("upchat-${version}-${buildType}.apk")
        }
    }
}

dependencies {
    implementation(fileTree(dir: 'libs', include: ['*.jar']))

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:34.5.0')

    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.firebaseui:firebase-ui-auth:9.1.1'
    implementation 'com.firebaseui:firebase-ui-database:9.1.1'
    implementation 'com.firebaseui:firebase-ui-storage:9.1.1'

    implementation 'com.google.android.material:material:1.13.0'

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.activity:activity-ktx:1.11.0'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.9.4'
    implementation 'androidx.security:security-crypto-ktx:1.1.0'
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    // Download with dialog
    implementation 'com.koushikdutta.ion:ion:3.1.0'

    // custom progress dialog
    // implementation 'io.github.tashilapathum:please-wait:0.4.0'

    // For generating and scanning QR-Code
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    // Libsignal for
    // coreLibraryDesugaring 'org.signal:libsignal-android:0.71.0'
    // implementation 'org.signal:libsignal-client:0.71.0'

    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
}
